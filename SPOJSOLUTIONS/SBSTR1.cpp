// Problem: SBSTR1
// Submission ID: 26168282
// Language: 4632

init constants '\n' and '1' and '0'
++++++++++>
+++++++[>+++++++>+++++++<<-]>>-
init pairs counter
<<++++++++++++++++++++++++
loop for string pairs
[
    -
    read strings
    >>>>>>>>>>>>>>> ,>,>,>,>,>,>,>,>,>,>,,>,>,>,>, >,[-]<
    rebase strings
    <<<<<<<<<<<<<<<[-]
    ++++++++[
        >------>------>------>------>------>------>------
        >------>------>------>------>------>------>------>------
        <<<<<<<<<<<<<<<-
    ]
    init substr_counter and solution_not_found_flag
    >>>>>>>>>>>>>>>>>>>>>>>>>>[-]++++++>>[-]+
    <<
    loop for substrings
    [
        <<<<<<<<<<
        copy ys to tmp
        +>+>+>+>+<<<< [
            [-]
            <<<<<<<<<<<<<<<<<<<<[-]
            >>>>>>>>>>>>>>>[<<<<<<<<<<<<<<<+>>>>>>>>>>>>>>>>>>>>+<<<<<-]
            >>>>>[<<<<<+>>>>>-]
            >
        ]
        compare
        <+<+<+<+<+ [
            -
            <<<<<<<<<<<<<<<<<<<<<<<<<[-]
            <[-]
            >>>>>>[<<<<<<+>>>>>>-]+
            >>>>>[<<<<<<<<<<<->+>>>>>>>>>>-]
            <<<<<<<<<<[>>>>>>>>>>+<<<<<<<<<<-]
            <[>>>>>>-<<<<<<+[-]]
            >>>>>>>>>>>>>>>>>>>>>>>>>>
            >
        ]
        mul
        <+<+<+<+ [
            -
            <<<<<<<<<<<<<<<<<<<<
            <<[-]
            <[-]
            >>>[<<<+>>>-]
            <<<[
                >>[>+<<+>-]
                <[>+<-]
                <-
            ]
            >>>>>>>>>>>>>>>>>>>>>>>
            >
        ]
        check mul result
        <<<<<<<<<<<<<<<<<<<<<
        <[-]+
        <[-]
        >> [
            print 1 and break loop
            <<<<<<<<<<<<<. <<.>>
            >>>>>>>>>>>>>>>>>>>>>>>>>>>>>>>>>>>>>>>[-]+
            >>-
            <<<<<<<<<<<<<<<<<<<<<<<<<<<<<-
            >[<<+>>-]
        ]
        <<[>>+<<-]
        > [
            shift array
            >>>>>>>>>>>>>>>>>
            +>+>+>+>+>+>+>+>+
            <<<<<<<< [
                -
                <<<<<<<<<<<<<<<<<<<<<<<<<<<[-]
                >>>>>>>>>>>>[-]
                >[<+<<<<<<<<<<<<+>>>>>>>>>>>>>-]
                <<<<<<<<<<<<<[>>>>>>>>>>>>>+<<<<<<<<<<<<<-]
                >>>>>>>>>>>>>>>>>>>>>>>>>>>
                >
            ]
            <<<<<<<<
            <<<<<<<<<<<<<<<<<<-
        ]
        >>>>>>>>>>>>>>>>>>>>>>>>>>>-
    ]
    >>[
        -<<<<<<<<<<<<<<<<<<<<<<<<<<<<<<<<<<<<<<<<
        . <<<.>>> >>>>>>>>>>>>>>>>>>>>>>>>>>>>>>>>>>>>>>>>
    ]
    <<<<<<<<<<<<<<<<<<<<<<<<<<<<<<<<<<<<<<<<<<
]